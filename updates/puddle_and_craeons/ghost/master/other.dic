OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	//If you want to add anything to OnTranslate, like text replacement, I would put it after this point, generally. Just make sure it's before the TOSTR(_talk)
	
	TOSTR(_talk)
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	if currentstate == "gaming"; "%(pudgaming)%(craegaming)"
	else; "%(pud)%(crae)"
}

//Happens when the ghost is unminimized. You *can* include dialogue, but you don't *have* to. I have included this event because if you don't, it defaults to \0\s[0]\1\s[10], which you don't want in some cases, such as if your ghost uses different surfaces or has a different number of characters.
OnWindowStateRestore
{
	if currentstate == "gaming"; "%(pudgaming)%(craegaming)"
	else; "%(pud)%(crae)"
}

//Network update url. Make sure you have a / on the end of the url or it will give a warning every time users try to update! If you don't plan to use network updates you can remove this
//IF YOU'RE USING GITHUB FOR NETWORK UPDATES: DO NOT USE github.io URLS! It will cause you pain and suffering. USE raw.githubusercontent!!!
//To get the raw.githubusercontent link super easy:
//• Open your ghost's repo online
//• navigate to the ghost's folder (the top level one, with install.txt, readme.txt, etc)
//• Click on install.txt, or another text file on that level
//• Once on that page, click the "raw" button. You should see the url now starts with raw.githubusercontent
//• Copy that url exactly as is, but *leave off the "install.txt" or whatever file name at the end.*
//Tada! That's your url! Make sure you test it! The dev palette and script/network log can help with that
On_homeurl
{
	"https://raw.githubusercontent.com/Craeons/CraeonsUkagakaStuffs/refs/heads/main/updates/puddle_and_craeons/"
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Link label";	"https://link.example.com/link/to/whatever/here/"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Craeons' Tumblr";	"https://craeons.tumblr.com/"
	"deadstoats' Tumblr";	"https://deadstoats.tumblr.com/"
}

On_kero.recommendsites
{
	FormatLinks(recommendsites_kero)
}

//These are the websites linked in the first option of the kero's right click menu. Same as above
recommendsites_kero : array
{
	"Ukagaka Dream Team Wiki";	"https://ukagakadreamteam.com/wiki/home"
}

// SCRIPT TEST
OnTalkTest {
	//raise me with \![raise,OnTalkTest,test]
	"%(pud)The network update worked!\w8%(pudhapi) Yipee!"
}

//CHANGE STATUS
OnCurrentStateChange {
	//raise me with \![raise,OnCurrentStateChange,gaming]
	currentstate = reference0
}

OnPronounsChange {
	// reference0 = pronoun
	// reference1 = whether this is from first install or not
	case reference0 {
		when "he"; pronouns = "he"
		when "she"; pronouns = "she"
		when "it"; pronouns = "it"
		others; pronouns = "they"
	}
	
	if reference1 {
		"%(craethink)I see... %(they)/%(them)/%(theirs), its it?%(crae) How about a name?\![open,inputbox,OnNameTeach,-1]"
	}
}

OnNameTeach {
	if reference0 == ""; username = "anonymous"
	else; username = reference0
	
	case frenuser {
		when "craeons" {
			"%(pudhapi)Look, Crae, look! It's the big you! Did they come here to pick us...%(pudthink) Wait, they don't quite look like you. Maybe a bit sexier, even.%(craeangy)\f[height,-3](Don't flirt with my sexy alternate self while I'm right here...)%(pudhapi)\n\nShould I flirt with you in front of them instead, then?\e"
		}
		when "puddle" {
			"%(pudhapi)Oh hey, it's the big me! Or...%(pudhuh) Someone who looks very like me. %(pudthink)Or... Not really. Hmm...\n%(pudsmile)Let's rummage through their files to know for sure~%(craesmirk)Is everyone going by %(username) so attractive, or is it just you guys? Heehee.%(pud)\n\nNah, sorry, it's just the one and only us~\e"
		}
		when "rain" {
			"%(craehapi)Look, Puddle, it's Rainy~! Hiiii Rainy hiiiii~%(pudhapi)Rain hiiii hiiii look Rain look we're in da 'puter!!!!!\e"
		}
		when "sebi" {
			"%(crae)Hey there, Ketchup Twink.%(craesmirk) Yeah, that's right. I know what you are.%(pudhapi)Hiiiiiiiiiii Mr. Charmer~\e"
		}
		when "harune" {
			"%(crae)OMG, it's harune!%(craehapi) Hiiiii harune, hope your day was good~%(pudhapi)Hey hey hey hello haruney want me %(pudsmirk)to google some wolfguys for you...%(pudhapi) (giggling)\e"
		}
		when "pixel" {
			"%(pudhapi)Heya there puppyboy, looking handsome today~ Mwa~"
			"%(pudhapi)Hiiii there princess~ %(pudsmirk)Hehe, welcoming me onto your personal computer, are you?"
			--
			"%(craeblep)Get a room... %(craesmirk)oh, wait, I guess this kinda is one.\e"
		}
		when "cel" {
			"No way, it's our smartguy cel! Hello, cel, look, I'm da code now! Are you proud of me?%(craeangy)\f[height,-3](I'm the one who did the code, be prouder of me instead...)%(pudhapi)\n\n\f[height,-3](Don't worry, I'm soooo proud of you too~)\e"
		}
	}
	"%(pudhapi)Neat! Okay, we'll be calling you %(username) from now on.%(crae)Nice to meetcha, %(username)~"
}

//—————————————————————————————— User pronoun related ——————————————————————————————

they
{
	if pronouns == "male"; "he"
	elseif pronouns == "female"; "she"
	elseif pronouns == "it"; "it"
	else; "they"
}

them
{
	if pronouns == "male"; "him"
	elseif pronouns == "female"; "her"
	elseif pronouns == "it"; "it"
	else; "them"
}

their
{
	if pronouns == "male"; "his"
	elseif pronouns == "female"; "her"
	elseif pronouns == "it"; "its"
	else; "their"
}

theirs
{
	if pronouns == "male"; "his"
	elseif pronouns == "female"; "hers"
	elseif pronouns == "it"; "its"
	else; "theirs"
}

themself
{
	if pronouns == "male"; "himself"
	elseif pronouns == "female"; "herself"
	elseif pronouns == "it"; "itself"
	else; "themself"
}

//%(they) %(p('are','is'))
//You can change the name of this function if you want! I kept it short so it's fast to type
p
{
	if pronouns == "they"; _argv[0] //they are
	else; _argv[1] //he is/she is
}

frenuser {
	_username = TOLOWER(username)
	case _username {
		when "craeons", "craeon", "crae"; "craeons"
		when "puddle", "pudli", "pud", "deadstoats", "deadstoat"; "puddle"
		when "rain", "rainy", "raincoloredcat", "raincat"; "rain"
		when "sebi", "sebir", "sebiromanes", "sebiromane"; "sebi"
		when "harune", "harun", "haruneski", "harunefrog"; "harune"
		when "pixel", "pixelgamer", "pixelgamer109"; "pixel"
		when "cel", "celesteela", "celes"; "cel"
		others; "stranger"
	}
}

/*

An example of the above setup:
"%(they) %(p('were','was')) looking for that earlier, I'll ask if %(they)'%(p('ve','s')) found it."

If using they, the %(p) functions will return the first word specified, resulting in:
"they were looking for that earlier, I'll ask if they've found it."

If not using they, for example if using she, it will return the second specified word instead, resulting in:
"she was looking for that earlier, I'll ask if she's found it."

With this function, you can quickly customize words that need to change based on whether it should be singular or plural. Just remember; the %(p) function is set up so that the first word you specify matches with the 'they' labels used on the pronoun functions. You can always switch that around if you like, too.

*/

//This just makes %(username) match with the username determined by the SSP prefs, rather than having to use the %username environment variable which is *not* the same and which you can't do things like TOUPPER to.
//If you want to have the user be able to set a nickname or something, you may want to change username to a variable instead and get rid of this!