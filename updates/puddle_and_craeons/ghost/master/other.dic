OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	//If you want to add anything to OnTranslate, like text replacement, I would put it after this point, generally. Just make sure it's before the TOSTR(_talk)
	
	TOSTR(_talk)
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
	elseif reference0 == "h"; OnTalkTest //comment this out when releasing pls
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	if currentstate == "gaming"; "%(pudgaming)%(craegaming)"
	else; "%(pud)%(crae)"
}

//Happens when the ghost is unminimized. You *can* include dialogue, but you don't *have* to. I have included this event because if you don't, it defaults to \0\s[0]\1\s[10], which you don't want in some cases, such as if your ghost uses different surfaces or has a different number of characters.
OnWindowStateRestore
{
	if currentstate == "gaming"; "%(pudgaming)%(craegaming)"
	else; "%(pud)%(crae)"
}

//Network update url. Make sure you have a / on the end of the url or it will give a warning every time users try to update! If you don't plan to use network updates you can remove this
//IF YOU'RE USING GITHUB FOR NETWORK UPDATES: DO NOT USE github.io URLS! It will cause you pain and suffering. USE raw.githubusercontent!!!
//To get the raw.githubusercontent link super easy:
//• Open your ghost's repo online
//• navigate to the ghost's folder (the top level one, with install.txt, readme.txt, etc)
//• Click on install.txt, or another text file on that level
//• Once on that page, click the "raw" button. You should see the url now starts with raw.githubusercontent
//• Copy that url exactly as is, but *leave off the "install.txt" or whatever file name at the end.*
//Tada! That's your url! Make sure you test it! The dev palette and script/network log can help with that
On_homeurl
{
	"https://raw.githubusercontent.com/Craeons/CraeonsUkagakaStuffs/refs/heads/main/updates/puddle_and_craeons/"
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Link label";	"https://link.example.com/link/to/whatever/here/"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Craeons' Tumblr";	"https://craeons.tumblr.com/"
	"deadstoats' Tumblr";	"https://deadstoats.tumblr.com/"
}

On_kero.recommendsites
{
	FormatLinks(recommendsites_kero)
}

//These are the websites linked in the first option of the kero's right click menu. Same as above
recommendsites_kero : array
{
	"Ukagaka Dream Team Wiki";	"https://ukagakadreamteam.com/wiki/home"
}

// SCRIPT TEST
OnTalkTest {
	//raise me with \![raise,OnTalkTest,test]
	hourlytalking = 0
	librarymentioned = 0
	craeatebooksmentioned = 0
	bigyousmentioned = 0
	pudwritingmentioned = 0
	"%(pudsnooze)%(craesnooze)\e"
}

//CHANGE STATUS
OnCurrentStateChange {
	// raise me with \![raise,OnCurrentStateChange,gaming]
	currentstate = reference0
}

OnPronounsTeach {
	// reference0 = pronoun
	// reference1 = whether this is from first install or not
	_temp = reference0
	case reference0 {
		when "he", "male"; pronouns = "male"
		when "she", "female"; pronouns = "female"
		when "it", "object"; pronouns = "it"
		others; pronouns = "they"
	}
	
	if reference1 {
		"%(craethink)I see\w2.\w2.\w2. %(they)/%(them)/%(theirs),\w4 its it?\w8%(crae) How about a name?\![open,inputbox,OnNameTeach,-1]"
	} else {
		"%(crae)Okie dokie, it's %(they)/%(them)/%(theirs) from now on.\e"
	}
}

OnNameTeach {
	if reference0 == ""; username = "anonymous"
	else; username = reference0
	
	case frenuser {
		when "craeons" {
			"%(pudhapi)Look,\w4 Crae,\w4 look!\w8 It's the big you!\w8 Did they come here to pick us\w2.\w2.\w2.%(pudthink)\w8 Wait,\w4 they don't quite look like you.\w8 Maybe a bit sexier,\w4 even.\w4%(craeangy)\w4%(smalltxt)Don't flirt with my sexy alternate self while I'm right here\w2.\w2.\w2.\w4%(pudhapi)\w8\n\nShould I flirt with you in front of them instead,\w4 then?\e"
		}
		when "puddle" {
			"%(pudhapi)Oh hey,\w4 it's the big me!\w8 Or\w2.\w2.\w2.\w2%(pudhuh)\w8 Someone who looks very like me.\w8 %(pudthink)Or\w2.\w2.\w2.\w2 Not really.\w8 Hmm\w2.\w2.\w2.\w2\n\w8%(pudsmile)Let's rummage through their files to know for sure~\w4%(craesmirk)\w8Is everyone going by %(username) so attractive,\w4 or is it just you guys?\w8 Heehee.\w4\w8%(pud)\n\nNah,\w4 sorry,\w4 it's just the one and only us~\e"
		}
		when "rain" {
			"%(craehapi)Look,\w4 Puddle,\w4 it's Rainy~!\w8 Hiiii\w3 Rainy\w3 hiiiii~\w4\w8%(pudhapi)Rain hiiii hiiii \w3look Rain look we're in da 'puter!!!!!\e"
		}
		when "sebi" {
			"%(crae)Hey there,\w4 Ketchup Twink.\w8%(craesmirk) Yeah,\w4 that's right.\w8 I know what you are.\w4\w8%(pudhapi)Hiiiii\w2iii\w2i\w2i\w2i Mr. Charmer~\e"
		}
		when "harune" {
			"%(crae)OMG,\w4 it's harune!\w8%(craehapi) Hiiiii harune,\w4 hope your day was good~\w4\w8%(pudhapi)Hey hey hey hello haruney want me\w2 %(pudsmirk)to google some wolfguys for you\w2.\w2.\w2.\w8%(pudhapi) %(nartxt)(giggling)\e"
		}
		when "pixel" {
			"%(pudhapi)Heya there puppyboy,\w4 looking handsome today~\w8 Mwa~"
			"%(pudhapi)Hiiii there princess~\w8 %(pudsmirk)Hehe,\w4 welcoming me onto your personal computer,\w4 are you?"
			--
			"%(craeblep)Get a room\w2.\w2.\w2.\w8 %(craesmirk)Oh,\w4 wait,\w4 I guess this kinda is one.\e"
		}
		when "cel" {
			"%(pud)No way,\w4 it's our smartguy cel!\w8 Hello,\w4 cel,\w4 look,\w4 I'm da code now!\w8 %(pudhapi)Are you proud of me?\w8%(craeangy)\w4%(smalltxt)(I'm the one who did the code,\w4 be prouder of me instead\w2.\w2.\w2.\w2)\w4\w8%(pudhapi)\n\n%(smalltxt)(Don't worry,\w4 I'm soooo proud of you too~)\e"
		}
		others {
			"%(pudhapi)Neat!\w8 Okay,\w4 we'll be calling you %(username) from now on.\w8\w4%(crae)Nice to meetcha,\w4 %(username)~\e"
		}
	}
}

//—————————————————————————————— User pronoun related ——————————————————————————————

they
{
	if pronouns == "male"; "he"
	elseif pronouns == "female"; "she"
	elseif pronouns == "it"; "it"
	else; "they"
}

them
{
	if pronouns == "male"; "him"
	elseif pronouns == "female"; "her"
	elseif pronouns == "it"; "it"
	else; "them"
}

their
{
	if pronouns == "male"; "his"
	elseif pronouns == "female"; "her"
	elseif pronouns == "it"; "its"
	else; "their"
}

theirs
{
	if pronouns == "male"; "his"
	elseif pronouns == "female"; "hers"
	elseif pronouns == "it"; "its"
	else; "theirs"
}

themself
{
	if pronouns == "male"; "himself"
	elseif pronouns == "female"; "herself"
	elseif pronouns == "it"; "itself"
	else; "themself"
}

//%(they) %(p('are','is'))
//You can change the name of this function if you want! I kept it short so it's fast to type
p
{
	if pronouns == "they"; _argv[0] //they are
	else; _argv[1] //he is/she is
}

/*
An example of the above setup:
"%(they) %(p('were','was')) looking for that earlier, I'll ask if %(they)'%(p('ve','s')) found it."
*/

frenuser {
	_username = TOLOWER(username)
	case _username {
		when "craeons", "craeon", "crae"; "craeons"
		when "puddle", "pudli", "pud", "deadstoats", "deadstoat"; "puddle"
		when "rain", "rainy", "raincoloredcat", "raincat"; "rain"
		when "sebi", "sebir", "sebiromanes", "sebiromane"; "sebi"
		when "harune", "harun", "haruneski", "harunefrog"; "harune"
		when "pixel", "pixelgamer", "pixelgamer109"; "pixel"
		when "cel", "celesteela", "celes"; "cel"
		others; "stranger"
	}
}