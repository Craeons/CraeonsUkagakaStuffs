//Separate from the menus because you could put other interactions here, like poking or booping or punching or asking about various hotspots
OnMouseDoubleClick
{
	if reference5 == 0 //reference5 is the mouse button being clicked, in this case a left click
	{
		//reference3 is which character is being clicked
		//If you want to add double click interactions, reference4 will be the name of the hitbox the mouse is over
		if reference3 == 0
		{
			case reference4 {
				when "lefthorn" {
					"%(pud)\i[6]\e"
					{
						"%(pud)\i[6]Nya?\e"
						"%(pud)\i[6]Nyaw?\e"
						"%(pud)\i[6]?\e"
					}
				}
				when "righthorn" {
					"%(pud)\i[7]\e"
					"%(pud)\i[8]\e"
					{
						"%(pud)\i[7]Nyeh?\e"
						"%(pud)\i[7]Nya?\e"
					}
				}
				when "nose" {
					"\0%(nartxt)Boop!%(xnartxt)\n\n"
					--
					"%(pudangy)Nya?"
					"%(pud)Nya?"
					"%(pud)Nyaw?"
					"%(pud)?"
					--
					"\e"
				}
				others; OnSakuraMenu
			}
		}
		else
		{
			case reference4 {
				when "wing" {
					"%(crae)\i[6]\e"
					{
						"%(crae)\i[6]Mm?\e"
						"%(crae)\i[6]Piou?\e"
					}
				}
				when "nose" {
					"\1%(nartxt)Boop!%(xnartxt)\n\n"
					--
					"%(craeangy)Piou?"
					"%(craeangy)Nyeh."
					"%(craeangy)Hhrm."
					"%(craeangy).\w2.\w2."
					if (frenuser == "puddle"); "%(craeangy)You!!!\w8 I know you just like my reaction!\w8%(craeshy)\nHmph\w2.\w2.\w2."
					if (frenuser == "sebi"); "%(craeangy)Nyeh\w2.\w2.\w2.\w8 I'm gonna pour all your ketchup down the drain."
					--
					if (RAND(10) < 2); "\w4\w8%(pudhapi)Heh,\w4 cute reaction.\e"
					"\e"
				}
				others; OnKeroMenu
			}
		}
	}
}

OnMouseMove
{
	if reference4 != ""
	{
		stroke++
		
		if stroke % 40 == 0
		{
			if reference3 == 0; OnMouseMove.Sakura
			else; OnMouseMove.Kero
		}
	}
}

OnMouseMove.Sakura
{
	if reference4 == "head"
	{
		"%(pudhapi)*Purrr*\w2.\w2.\w2."
		"%(pudhapi)Cool,\w4 scratch around there more~"
		"%(pudhapi)Keep petting or I'll chomp off your fingers~"
		{
			"%(pudhapi)A lil bit to the right\w2.\w2.w2."
			"%(pudhapi)A lil bit to the left\w2.\w2.\w2."
		}
		"%(pudhapi)Why are you petting me?\w8 Do I look like a cat to you?\w4\w8%(craeuhh)As opposed to what,\w4 a dog\w2.\w2.\w2.\w2?\e"
		--
		if (RAND(10) < 5) {
			"\w8%(craeangy)\w4\f[height,-5]"
			--
			"(Hrmph\w2.\w2.\w2.\w8 I wanna get petted too\w2.\w2.\w2.\w2)\e"
			"(Why can't \w3\f[italic,1]I\f[italic,default]\w3 get some wing scritchies?)\e"
			"(Looks comfy\w2.\w2.\w2.\w2)\e"
		}
	}
}

OnMouseMove.Kero
{
	if reference4 == "head"
	{
		"%(craehapi)Mmm... Right around the feathers, please~ â™¥"
		"%(craehapi)*Peep peep*"
		"%(craehapi)Wehhheheh\w2.\w2.\w2."
		"%(craehapi)Comfy\w2.\w2.\w2."
		--
		if (RAND(10) < 3) {
			"\w8%(pudcri)\w4\f[height,-5]"
			--
			"(Hey, I want petting too...)\e"
			"(It looks comfy...)\e"
			"(Do they like it more than \w3\f[italic,1]my\f[italic,default]\w3 petting and scritchies?)\e"
		}
	}
}

OnMouseLeaveAll
{
	stroke = 0
}